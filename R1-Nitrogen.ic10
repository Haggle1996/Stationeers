# pressurize to 38MPa, fill at 30MPa
# Only open valve when pressure above 30MPa
#  and 0% n.
alias AC d0
alias Analyzer d1
alias Pump d4
alias Valve d5

define maxPressure 38000
define minPressure 32000

alias pressure r0
alias n r1
alias isOpen r15

init:
s AC Setting 163.15 # -110C
s AC On 1
s Pump Setting 10
s Pump On 0
s Valve On 0

main:
yield
s AC Mode 1

jal valveCheck
jal pressurize

j main

pressurize:
l isOpen Valve On # valve must be closed
bnez isOpen pressurize_loop_end
l pressure Analyzer Pressure # pressure below min
bge pressure minPressure pressurize_loop_end
# no more then 1L liquid
l n Analyzer VolumeOfLiquid
bgt n 1.0 pressurize_loop_end
s Pump On 1
pressurize_loop:
l pressure Analyzer Pressure # pressure above max
bge pressure maxPressure pressurize_loop_end
l n Analyzer VolumeOfLiquid
bgt n 1.0 pressurize_loop_end # more than a liter
yield
j pressurize_loop
pressurize_loop_end:
s Pump On 0
j ra

valveCheck:
l pressure Analyzer Pressure 
sgt pressure pressure minPressure # pressure > min
l n Analyzer RatioNitrogen 
seqz n n                      # 0% gas n
and isOpen pressure n
l n Analyzer VolumeOfLiquid
seqz n n                      # 0% liq n
and isOpen isOpen n 
l pressure Pump On 
seqz pressure pressure            # pump is off
and isOpen isOpen pressure
s Valve On isOpen
breqz isOpen 2
sleep 5
j ra